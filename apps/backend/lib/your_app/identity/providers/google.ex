defmodule YourApp.Identity.Providers.Google do
  @moduledoc false

  alias YourApp.Identity.ProviderClaims
  alias YourApp.Identity.Providers.GoogleJwksCache
  alias YourApp.Identity.Providers.JwtVerifier

  def verify(%{"provider_token" => provider_token}), do: verify(%{provider_token: provider_token})

  def verify(%{provider_token: provider_token})
      when is_binary(provider_token) and provider_token != "" do
    with {:ok, provider_config} <- provider_config(),
         {:ok, %{header: header, claims: claims}} <-
           JwtVerifier.verify(provider_token, provider_config),
         {:ok, subject} <- required_claim(claims, "sub"),
         {:ok, email} <- required_claim(claims, "email") do
      {:ok,
       %ProviderClaims{
         subject: subject,
         email: email,
         display_name: display_name(claims),
         provider_data: %{
           "aud" => claims["aud"],
           "email_verified" => Map.get(claims, "email_verified"),
           "iss" => claims["iss"],
           "kid" => header["kid"]
         }
       }}
    end
  end

  def verify(_params), do: {:error, :invalid_provider_token}

  defp provider_config do
    provider_config = YourApp.Auth.provider_config(:google)

    case Keyword.fetch(provider_config, :jwks) do
      {:ok, _jwks} ->
        {:ok, provider_config}

      :error ->
        with {:ok, jwks} <- GoogleJwksCache.get() do
          {:ok, Keyword.put(provider_config, :jwks, jwks)}
        end
    end
  end

  defp display_name(claims) do
    case claims["name"] do
      name when is_binary(name) and name != "" -> name
      _name -> claims["email"] |> String.split("@") |> List.first() |> humanize()
    end
  end

  defp required_claim(claims, claim_name) do
    case claims[claim_name] do
      value when is_binary(value) and value != "" -> {:ok, value}
      _value -> {:error, :invalid_provider_token}
    end
  end

  defp humanize(value) do
    value
    |> String.replace("-", " ")
    |> String.split(" ", trim: true)
    |> Enum.map_join(" ", &String.capitalize/1)
  end
end
